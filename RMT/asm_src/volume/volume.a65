;
; VOLUME CONTROL
; example by raster/c.p.u., 2008
;
;
STEREOMODE	equ 0				;0 => compile RMTplayer for mono 4 tracks
;								;1 => compile RMTplayer for stereo 8 tracks
;								;2 => compile RMTplayer for 4 tracks stereo L1 R2 R3 L4
;								;3 => compile RMTplayer for 4 tracks stereo L1 L2 R3 R4
;
;
	icl "rmtplayr.a65"			;include RMT player routine
;
;
	opt h-						;RMT module is standard Atari binary file already
	ins "song.rmt"				;include music RMT module
	opt h+
;
;
MODUL	equ $4000				;address of RMT module
KEY		equ $2fc				;keypressed code
;
	org $6000
start
;
	ldx #<text
	ldy #>text
	jsr $c642					;print info text to screen
;
	lda #0						;#0 => maximal volume initialization
	sta RMTGLOBALVOLUMEFADE
;
	ldx #<MODUL					;low byte of RMT module to X reg
	ldy #>MODUL					;hi byte of RMT module to Y reg
	lda #0						;starting song line 0-255 to A reg
	jsr RASTERMUSICTRACKER		;Init
;
	ldy #<vbi
	ldx #>vbi
	lda #$07
	jsr $e45c					;Start VBI routine
;
;
loop
	lda #255
	sta KEY						;no key pressed
;
waitkey
	lda KEY						;keycode
	cmp #255
	beq waitkey					;no key pressed
;
	and #63
	cmp #14						;keycode '-' (cursor up)
	bne nk2
;volume up
	lda RMTGLOBALVOLUMEFADE
	beq loop					;maximal volume?
	sec
	sbc #$10					;volume up => decrease!
	sta RMTGLOBALVOLUMEFADE
	jmp loop
;
nk2	cmp #15						;keycode '=' (cursor down)
	bne nk3
;volume down
	lda RMTGLOBALVOLUMEFADE
	clc
	adc #$10					;volume down => increase!
	bcs loop					;minimal volume
	sta RMTGLOBALVOLUMEFADE
	jmp loop
;
nk3
	jmp loop
;
;
;
vbi
;
	jsr RASTERMUSICTRACKER+3	;1 play
;
	jmp $e462					;end vbi
;
;
;
text dta c'Press ',$1b,$9c,c'/',$1b,$9d,c' for volume up/down',$9b
;
;
;
	run start					;run addr
;
;that's all... :-)